import java.nio.file.Paths

import static org.apache.tools.ant.taskdefs.condition.Os.FAMILY_WINDOWS
import static org.apache.tools.ant.taskdefs.condition.Os.isFamily

task installPipenv {
    doLast {
        exec {
            commandLine 'pip', 'install', 'pipenv==2018.11.26'
        }
    }
}

task pythonVersionInfo {
    doLast {
        exec {
            commandLine 'python', '--version'
        }
        exec {
            commandLine 'pip', '--version'
        }
        exec {
            commandLine 'pipenv', '--version'
        }
        exec {
            // NB: the python version inside pipenv is not necessarily the same as the version outside pipenv
            commandLine 'pipenv', 'run', 'python', '--version'
        }
        exec {
            // NB: the pip version inside pipenv is not necessarily the same as the version outside pipenv
            commandLine 'pipenv', 'run', 'pip', '--version'
        }
    }
}

task installDependencies {
    doLast {
        exec {
            commandLine 'pipenv', 'install', '--dev', '--python', '3.6'
        }
    }
}

task preparePythonEnvironment {
    dependsOn(installPipenv)
    dependsOn(installDependencies)
    dependsOn(":keanu-project:preparePythonClasspath")
    dependsOn(":codegen:runCodeGeneration")
}

task pytest {
    mustRunAfter preparePythonEnvironment // this is a dependency but only needs to be run once

    doLast {
        exec {
            commandLine 'pipenv', 'run', 'mypy', '--ignore-missing-imports', '--disallow-untyped-calls', 'keanu', 'tests', 'examples'
        }
        exec {
            commandLine 'pipenv', 'run', 'pytest', '--doctest-modules', 'keanu'
        }
        exec {
            // workaround for bug: this hits an NSP error if it's not run first
            commandLine 'pipenv', 'run', 'pytest', 'tests/test_gradient_optimization.py'
        }
        exec {
            // workaround for bug: this hits an NSP error if it's not run first
            commandLine 'pipenv', 'run', 'pytest', 'tests/test_lorenz.py'
        }
        exec {
            commandLine 'pipenv', 'run', 'pytest', '--ignore=tests/test_gradient_optimization.py', '--ignore=tests/test_lorenz.py'
        }
    }
}

task generateDocumentation {
    group "pythonDocumentation"
    dependsOn(":codegen:runCodeGeneration")
    doLast {
        exec {
            workingDir("../docs/")
            commandLine "rm", "-rf", "python/latest/"
        }
        exec {
            workingDir("docs/")
            commandLine "pipenv", "run", "make", "html"
        }
        exec {
            // This ensures that there are no directories prefixed with an underscore
            // because Jekyll will ignore these directories which causes the generated docs not to be served properly
            workingDir("docs/")
            commandLine "pipenv", "run", 'python', 'remove_underscores.py'
        }
        exec {
            commandLine 'mv', 'docs/_build/html/', '../docs/python/latest/'
        }
        exec {
            workingDir("docs/")
            commandLine 'rm', '-rf', '_build/'
        }
    }
}

task formatApply {
    mustRunAfter preparePythonEnvironment // this is a dependency but only needs to be run once
    mustRunAfter ':spotlessPythonApply'   // spotless does some checks of the python code too

    doLast {
        exec {
            commandLine 'pipenv', 'run', 'yapf', '--in-place', '--recursive', '--parallel', project.relativeProjectPath(".")
        }
    }
}

task formatCheck {
    mustRunAfter preparePythonEnvironment // this is a dependency but only needs to be run once
    mustRunAfter ':spotlessPythonCheck'   // spotless does some checks of the python code too

    doLast {
        exec {
            commandLine 'pipenv', 'run', 'yapf', '--diff', '--recursive', '--parallel', project.relativeProjectPath(".")
        }
    }
}

task buildWheelDistribution {
    mustRunAfter preparePythonEnvironment
    mustRunAfter preparePythonEnvironment

    doLast {
        exec {
            commandLine 'pipenv', 'run', 'python', project.file("setup.py"), 'sdist', 'bdist_wheel'
        }
    }
}

pythonVersionInfo.mustRunAfter(installPipenv)
installDependencies.dependsOn(installPipenv)
generateDocumentation.dependsOn(preparePythonEnvironment)
test.dependsOn(preparePythonEnvironment)
test.dependsOn(formatCheck)
test.dependsOn(pytest)

buildWheelDistribution.dependsOn(test)
