plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

dependencies {
    implementation 'org.reflections:reflections:0.9.11'
    implementation 'com.google.code.gson:gson:2.7'
    implementation 'org.freemarker:freemarker:2.3.28'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    implementation 'org.slf4j:slf4j-api:1. 8.0-beta2'
    implementation 'org.slf4j:slf4j-log4j12:1.8.0-beta2'

    compile project(':keanu-project')
    compile files("${System.getProperty('java.home')}/../lib/tools.jar")

    //testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.19.1'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
}

task saveJavadocStringsToFile(type: Javadoc) {
    source = project(':keanu-project').sourceSets.main.allJava

    def docletClasspath = rootProject.compileJava.classpath.files.toList()
    docletClasspath.add(new File(layout.projectDirectory.asFile.getPath() + "/build/classes/java/main"))
    options.setDocletpath(docletClasspath)
    options.setDoclet("io.improbable.keanu.codegen.python.KeanuProjectDoclet")

    classpath = project(':keanu-project').sourceSets.main.runtimeClasspath
    classpath += project(':keanu-project').sourceSets.main.compileClasspath
}

task runCodeGeneration (type: JavaExec) {
    dependsOn saveJavadocStringsToFile
    classpath sourceSets.main.runtimeClasspath
    main = 'io.improbable.keanu.codegen.python.Runner'
    args += project.rootDir.toString() + '/keanu-python/keanu/vertex/'
}

task codeGen {
    dependsOn(runCodeGeneration)
    dependsOn(":keanu-python:generateDocumentation")
}

lombok { // optional: values below are the defaults
    version = "1.16.20"
    sha256 = ""
}